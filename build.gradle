plugins {
    id 'java'
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'
    copyIdeResources = true

    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "debug"

            // --- MIXIN REMAP (needed for external mods in userdev) ---
            property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", file("build/createSrgToMcp/output.srg").absolutePath//
            // (optional) see more in console if it still fails
            // property "mixin.debug.verbose", "true"

            mods {
                combatflask {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("runServer")
            property "forge.logging.console.level", "debug"

            property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", file("build/createSrgToMcp/output.srg").absolutePath

            mods {
                combatflask {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("runData")

            args "--mod", "combatflask", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")

            property "mixin.env.remapRefMap", "true"
            property "mixin.env.refMapRemappingFile", file("build/createSrgToMcp/output.srg").absolutePath

            mods {
                combatflask {
                    source sourceSets.main
                }
            }
        }
    }
}
repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = 'https://maven.parchmentmc.org/' }
    maven {
        name = "Curios Maven"
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name = "Iron's Spells Maven"
        url = "https://maven.ironsspellbooks.io/releases"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.3.0"

    // Curios API (d√©pendance requise)
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.9.1+1.20.1:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.9.1+1.20.1")

    // Iron's Spellbooks
    implementation fg.deobf("io.redspace:irons_spellbooks:1.20.1-3.4.0")
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

sourceSets.main.resources {
    srcDir "src/generated/resources"
}